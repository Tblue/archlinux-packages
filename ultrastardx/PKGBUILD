# Maintainer: Tilman Blumenbach <tilman+aur@ax86.net>

pkgname=ultrastardx
pkgver=2017.8.0
pkgrel=3
pkgdesc="Free and open source karaoke singing game"
url="https://usdx.eu/"
license=(GPL2)
source=(
    "git+https://github.com/UltraStar-Deluxe/USDX.git#tag=v${pkgver:?}"
)
sha384sums=('SKIP')
arch=(x86_64)

# NB: libbass is not needed by the Linux build.
depends=(
    sdl2_image ffmpeg2.8 sqlite portaudio pcre lua opencv projectm
    ttf-font
)
makedepends=("fpc>=3.0.0" git gcc)


prepare() {
    local crtend_path="$(gcc -print-file-name=crtendS.o)"
    [ -z "${crtend_path?}" ] && return 2

    local PFLAGS_EXTRA="-Fl$(dirname "${crtend_path:?}")"
    if [ "${LDFLAGS:0:4}" = "-Wl," ]; then
        local linker_flags="${LDFLAGS:3}"
        PFLAGS_EXTRA="${PFLAGS_EXTRA?} ${linker_flags//,/ -k}"
        unset LDFLAGS
    fi

    cd USDX
    ./autogen.sh
    ./configure --prefix=/usr --with-libprojectM \
        CC=gcc CXX="g++" \
        PKG_CONFIG_PATH=/usr/lib/ffmpeg2.8/pkgconfig \
        PFLAGS_EXTRA="${PFLAGS_EXTRA:?}"
}

build() {
    cd USDX
    make
}

package() {
    cd USDX
    make DESTDIR="${pkgdir:?}" install
}
